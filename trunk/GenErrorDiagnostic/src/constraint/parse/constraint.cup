// CUP specification for a set-based constraint language (no actions)
package constraint.parse;

import java.util.ArrayList;
import java.util.HashMap;
import java.util.HashSet;
import java.util.List;
import java.util.Map;
import java.util.Set;

import constraint.ast.Bottom;
import constraint.ast.Constraint;
import constraint.ast.Constructor;
import constraint.ast.ConstructorElement;
import constraint.ast.Element;
import constraint.ast.Environment;
import constraint.ast.JoinElement;
import constraint.ast.MeetElement;
import constraint.ast.Relation;
import constraint.ast.Top;
import constraint.ast.Variable;
import constraint.ast.Position;
import diagnostic.DiagnosisInput;

action code {: 
				Map<String, Constructor> constructors = new HashMap<String, Constructor>( );
				Map<String, Variable> variables = new HashMap<String, Variable>(); 
				Environment env = new Environment();
			:};
						
/* Terminals (tokens returned by the scanner). */
terminal 			SEPERATION;
terminal            CONSTRUCTOR;
terminal            EQ, LEQ, GEQ, SEMICOLON, ARROW, LARROW, LPAREN, RPAREN, LBRACK, RBRACK, COMMA, JOIN, MEET, DASH;
terminal Integer    INTEGER_LITERAL;
terminal String     IDENTIFIER, STRING_LITERAL;

/* Non terminals */
non terminal DiagnosisInput     cons_spec;
non terminal Set<Constraint>	equations;
non terminal Environment 		env;
non terminal 					constructors, constructor;
non terminal				    assumptions;
non terminal Constraint 		equation;
non terminal Element			term, term_pos, element, cons;
non terminal String           	id;
non terminal Integer			arity;
non terminal List<Element>		element_list;
non terminal Position           range, position;

/* Precedences */
precedence right ARROW, LARROW, MEET, JOIN;
precedence left  COMMA;

/* The grammar */
cons_spec 	::= constructors 	
			{: 
					constructors.put("arrow", new Constructor("arrow", 2, false));
					constructors.put("larrow", new Constructor("larrow", 2, true));
					constructors.put("pair",  new Constructor("pair", 2, false));
					constructors.put("_",  new Bottom()); 
					constructors.put("*",  new Top()); 
			:} 
				SEPERATION assumptions SEPERATION equations:l
			{: RESULT = new DiagnosisInput (env, l); :}			
			| 	constructors 
			{: 
					constructors.put("arrow", new Constructor("arrow", 2, false));
					constructors.put("larrow", new Constructor("larrow", 2, true));
					constructors.put("pair",  new Constructor("pair", 2, false)); 
					constructors.put("_",  new Bottom()); 
					constructors.put("*",  new Top()); 
			:} 
				equations:l
			{: RESULT = new DiagnosisInput (env, l); :}
			;

constructors ::= constructors constructor
			| constructor;
			
constructor  ::= CONSTRUCTOR id:s arity:a
			{: constructors.put( s, new Constructor(s, a, false)); :}
			;

id        	::= IDENTIFIER:s
			{: RESULT = s; :} 
			;
			
arity		::= INTEGER_LITERAL:a
			{: RESULT = a; :};
			
assumptions ::= assumptions equation:e
            {: env.addAssertion(e); :}
            | 
            equation:e
            {: env.addAssertion(e); :}
            ;

equations 	::= equations:s equation:e 
            {: s.add(e); RESULT = s; :}
            | 
            equation:e
            {: Set<Constraint> set = new HashSet<Constraint>(); set.add(e); RESULT = set; :}
            ;
            
equation 	::= term_pos:e1 EQ term_pos:e2 env:e SEMICOLON position:p
			{: RESULT = new Constraint (e1, e2, Relation.EQ, e, p); :}  
            | term_pos:e1 LEQ term_pos:e2 env:e SEMICOLON position:p
         	{: RESULT = new Constraint (e1, e2, Relation.LEQ, e, p); :}
            | term_pos:e1 GEQ term_pos:e2 env:e SEMICOLON  position:p
            {: RESULT = new Constraint (e2, e1, Relation.LEQ, e, p); :}
            ;
            
env         ::= LBRACK equations:l RBRACK
			{: Environment e = new Environment(); 
			   for (Constraint c : l)
			       e.addAssertion(c);
			   RESULT = e; 
			:}
			| LBRACK RBRACK
			{:
			   RESULT = new Environment();
			:}
			|
			{:
			   RESULT = new Environment();
			:}
			;
						
position	::= LBRACK STRING_LITERAL:name range:p RBRACK
			{: 
			   if (p!=null) p.setFileName(name); 
			   RESULT = p; 
			:}
			| LBRACK range:p RBRACK
			{: 
			   RESULT = p; 
			:}
			;
			
range		::= INTEGER_LITERAL:d1 COMMA INTEGER_LITERAL:d2 DASH INTEGER_LITERAL:d3
			{:
			    RESULT = new Position ("", d1, d2, d1, d3);
			:}
			|   INTEGER_LITERAL:d1 COMMA INTEGER_LITERAL:d2 DASH INTEGER_LITERAL:d3 COMMA INTEGER_LITERAL:d4
			{:
			    RESULT = new Position ("", d1, d2, d3, d4);
			:}
			|   IDENTIFIER:s /* identifiers are currently ignored */
			{:  RESULT = Position.EmptyPosition(); :}
			| /* no position */
			{:  RESULT = Position.EmptyPosition(); :}
			;
            
term_pos    ::= term:t1 position:p 
			{:
				t1.setPosition(p);
				RESULT = t1;
			:}
			;

term     	::=  term:t1 ARROW term:t2
			{: 	List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new ConstructorElement ("", constructors.get("arrow"), l);
			:}
			|
				term:t1 LARROW term:t2
			{: 	List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new ConstructorElement ("", constructors.get("larrow"), l);
			:}
			| term:t1 COMMA term:t2
			{: 	
				List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new ConstructorElement ("", constructors.get("pair"), l); 
			:}
			| term:t1 JOIN term:t2
			{: 	
				List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new JoinElement ("", l); 
			:}
			| term:t1 MEET term:t2
			{: 	
				List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new MeetElement ("", l); 
			:}
			| cons:cons element_list:l
			{: 	Constructor c = (Constructor) cons;
				if (c.getArity() != l.size()) throw new Error("Constructor "+c+" takes wrong number of parameters: "+
			   						"expecting "+c.getArity()+" getting "+l.size());
			    RESULT = new ConstructorElement ("", c, l); 
			:}
			| element:e
			{: RESULT = e; :}
			;
		
element_list ::= element_list:l element:t
			{: l.add(t); RESULT = l; :}
			| element:e
			{: List<Element> l = new ArrayList<Element>();  l.add(e); RESULT = l; :}
			;
			
cons		::= IDENTIFIER:s
			{:
				if (constructors.containsKey(s)) {
					RESULT = constructors.get(s).getInstance();
				} 
			   	else throw new Error("\""+s+"\"" + " is not a constructor");
			:}
			;

/* a single element is either a variable or a constructor with 0 arity */
element		::= IDENTIFIER:s
			{: 	
				if (constructors.containsKey(s)) {
					RESULT = constructors.get(s).getInstance();
				} 
			   	else {
			   		if (!(variables.containsKey(s)))
			   			variables.put (s, new Variable(s,Position.EmptyPosition()));
			   		RESULT = variables.get(s);
			   	}			   	  
			 :}
			 | LPAREN term:t RPAREN 
			 {:  RESULT = t; :}
			 ;