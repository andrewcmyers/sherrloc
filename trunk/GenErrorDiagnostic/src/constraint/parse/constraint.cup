// CUP specification for a set-based constraint language (no actions)
package constraint.parse;

import java.util.ArrayList;
import java.util.HashMap;
import java.util.HashSet;
import java.util.List;
import java.util.Map;
import java.util.Set;

import constraint.ast.Bottom;
import constraint.ast.Constraint;
import constraint.ast.Constructor;
import constraint.ast.ConstructorElement;
import constraint.ast.Element;
import constraint.ast.Environment;
import constraint.ast.JoinElement;
import constraint.ast.MeetElement;
import constraint.ast.Relation;
import constraint.ast.Top;
import constraint.ast.Variable;
import diagnositc.DiagnosisInput;

action code {: 
				Map<String, Constructor> constructors = new HashMap<String, Constructor>( );
				Map<String, Variable> variables = new HashMap<String, Variable>(); 
				Environment env = new Environment();
			:};
						
/* Terminals (tokens returned by the scanner). */
terminal 			SEPERATION;
terminal            CONSTRUCTOR, LABEL;
terminal            EQ, LEQ, GEQ, SEMICOLON, ARROW, LPAREN, RPAREN, LBRACK, RBRACK, COMMA, SEP, STAR, AT, JOIN, MEET;
terminal Integer    INTEGER_LITERAL;
terminal String     IDENTIFIER, STRING_LITERAL;

/* Non terminals */
non terminal DiagnosisInput     cons_spec;
non terminal Set<Constraint>	equations;
non terminal Environment 		env;
non terminal 					constructors, constructor;
non terminal				    assumptions, actsfor_list, actsfor;
non terminal Constraint 		equation;
non terminal Element			term, element, cons;
non terminal String           	id, position;
non terminal Integer			arity;
non terminal List<Element>		element_list;

/* Precedences */
precedence right ARROW, MEET, JOIN;
precedence left  STAR;

/* The grammar */
cons_spec 	::= constructors 	
			{: 
					constructors.put("arrow", new Constructor("arrow", 2));
					constructors.put("pair",  new Constructor("pair", 2));
					constructors.put("_",  new Bottom()); 
					constructors.put("*",  new Top()); 
			:} 
				SEPERATION assumptions SEPERATION equations:l
			{: RESULT = new DiagnosisInput (env, l); :}			
			| 	constructors 
			{: 
					constructors.put("arrow", new Constructor("arrow", 2));
					constructors.put("pair",  new Constructor("pair", 2)); 
					constructors.put("_",  new Bottom()); 
					constructors.put("*",  new Top()); 
			:} 
				equations:l
			{: RESULT = new DiagnosisInput (env, l); :}
			;

constructors ::= constructors constructor
			| constructor;
			
constructor  ::= CONSTRUCTOR id:s arity:a
			{: constructors.put( s, new Constructor(s, a)); :}
			;

id        	::= IDENTIFIER:s
			{: RESULT = s; :} 
			;
			
arity		::= INTEGER_LITERAL:a
			{: RESULT = a; :};
			
assumptions ::= assumptions equation:e
            {: env.addAssertion(e); :}
            | 
            equation:e
            {: env.addAssertion(e); :}
            ;

equations 	::= equations:s equation:e 
            {: s.add(e); RESULT = s; :}
            | 
            equation:e
            {: Set<Constraint> set = new HashSet<Constraint>(); set.add(e); RESULT = set; :}
            ;
            
equation 	::= term:e1 EQ term:e2 env:e SEMICOLON position:p
			{: RESULT = new Constraint (e1, e2, Relation.EQ, e, p); :}  
            | term:e1 LEQ term:e2 env:e SEMICOLON position:p
         	{: RESULT = new Constraint (e1, e2, Relation.LEQ, e, p); :}
            | term:e1 GEQ term:e2 env:e SEMICOLON  position:p
            {: RESULT = new Constraint (e2, e1, Relation.LEQ, e, p); :}
            ;
            
env         ::= LBRACK equations:l SEP actsfor_list RBRACK
			{: Environment e = new Environment(); 
			   for (Constraint c : l)
			       e.addAssertion(c);
			   RESULT = e; 
			:}
			|
			LBRACK equations:l RBRACK
			{: Environment e = new Environment(); 
			   for (Constraint c : l)
			       e.addAssertion(c);
			   RESULT = e; 
			:}
			| LBRACK RBRACK
			{:
			   RESULT = new Environment();
			:}
			|
			{:
			   RESULT = new Environment();
			:}
			;
			
actsfor_list::= actsfor_list actsfor
			| actsfor
			;
			
actsfor     ::= id:s1 EQ id:s2 SEMICOLON
			{: env.addActsFor(s1,s2); env.addActsFor(s2,s1); :}  
            | id:s1 LEQ id:s2 SEMICOLON
         	{: env.addActsFor(s2,s1); :}
            | id:s1 GEQ id:s2 SEMICOLON
            {: env.addActsFor(s1,s2); :}
            ;
			
position	::= AT STRING_LITERAL:s
			{: RESULT = s; :}
			| /* no information */
			{: RESULT = ""; :}
			;
            
term     	::=  term:t1 ARROW term:t2
			{: 	List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new ConstructorElement ("", constructors.get("arrow"), l);
			:}
			| term:t1 STAR term:t2
			{: 	
				List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new ConstructorElement ("", constructors.get("pair"), l); 
			:}
			| term:t1 JOIN term:t2
			{: 	
				List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new JoinElement ("", l); 
			:}
			| term:t1 MEET term:t2
			{: 	
				List<Element> l = new ArrayList<Element>();
				l.add(t1);
				l.add(t2);  
				RESULT = new MeetElement ("", l); 
			:}
			| cons:cons element_list:l
			{: 	Constructor c = (Constructor) cons;
				if (c.getArity() != l.size()) throw new Error("Constructor "+c+" takes wrong number of parameters: "+
			   						"expecting "+c.getArity()+" getting "+l.size());
			    RESULT = new ConstructorElement ("", c, l); 
			:}
			| element:e
			{: RESULT = e; :}
			;
		
element_list ::= element_list:l element:t
			{: l.add(t); RESULT = l; :}
			| element:e
			{: List<Element> l = new ArrayList<Element>();  l.add(e); RESULT = l; :}
			;
			
cons		::= IDENTIFIER:s
			{:
				if (constructors.containsKey(s)) {
					RESULT = constructors.get(s);
				} 
			   	else throw new Error("\""+s+"\"" + " is not a constructor");
			:}
			;

/* a single element is either a variable or a constructor with 0 arity */
element		::= IDENTIFIER:s position:p
			{: 	
				if (constructors.containsKey(s)) {
					RESULT = constructors.get(s);
				} 
			   	else {
			   		if (!(variables.containsKey(s)))
			   			variables.put (s, new Variable(s,p));
			   		RESULT = variables.get(s);
			   	}			   	  
			 :}
			 | LPAREN term:t RPAREN 
			 {:  RESULT = t; :}
			 ;